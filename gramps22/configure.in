dnl $Id$

dnl Process this file with autoconf to produce a configure script.
dnl May need to run automake && aclocal first

AC_PREREQ(2.57)
AC_INIT(gramps, 2.2.11, [gramps-bugs@lists.sourceforge.net])
AC_CONFIG_SRCDIR(configure.in)
AM_INIT_AUTOMAKE(1.6.3)

AC_CONFIG_MACRO_DIR([m4])
GNOME_DOC_INIT

RELEASE=0.SVN$(svnversion -n .)
dnl RELEASE=0rc1
dnl RELEASE=1

VERSIONSTRING=$VERSION
if test x"$RELEASE" != "x"
then
  VERSIONSTRING="$VERSION-$RELEASE"
fi

dnl put the ACLOCAL flags in the Makefile
ACLOCAL="$ACLOCAL $ACLOCAL_FLAGS"

AC_PROG_INTLTOOL

dnl Checking for gconftool-2
AC_PATH_PROG(GCONFTOOL, gconftool-2, no)
if test "x$GCONFTOOL" = xno; then
  AC_MSG_ERROR([gconftool-2 executable not found in your path - should be installed with GConf])
fi

AM_GCONF2_REPLACEMENT
AM_SHARED_MIME
AM_PACKAGER
dnl This is a hack to disable scrollkeeper update when in the packager mode.
dnl It may not seem good to purists, since we're defining
dnl ENABLE_SK_{TRUE,FALSE} twice, but I cannot see any other way
dnl without tweaking the gnome-doc-utils macros manually.
dnl So, scrollkeeper update is enabled when it would normally be,
dnl *provided* this is not the packager mode. Packager mode cancels it.
AM_CONDITIONAL(ENABLE_SK, test "x$packager_mode" = "xfalse" -a "x$enable_scrollkeeper" = "xyes")
AC_SUBST(RELEASE)
AC_SUBST(VERSIONSTRING)

dnl These are needed for the full paths for the manual output
HELPPATH=$(pwd)/help/output
FONTPATH=/usr/share/fonts/truetype/freefont
AC_SUBST(HELPPATH)
AC_SUBST(FONTPATH)

dnl Add the languages which your application supports here.
ALL_LINGUAS="hu zh_CN cs da de es fr it nb nl no pl pt_BR ro ru sv eo fi lt sk tr sl bg"
GETTEXT_PACKAGE=gramps
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE", [Define to the Gettext package name.])
AM_GLIB_GNU_GETTEXT

dnl Checks for programs.  
dnl We first only check for python >= 2.3

AM_PATH_PYTHON(2.3)
AC_PATH_PROG(BINSH, sh)

pygtk_require="
try:
    import pygtk
    pygtk.require('2.0')
except ImportError:
    pass

def out(line):
    f = open('conftest.out', 'w')
    f.write(line)
    f.close()
"

AC_MSG_CHECKING(Python bindings for gtk2.8 (pygtk2>=2.8.0))
cat > conftest.py <<EOF
$pygtk_require
try:
    import gobject
    version = gobject.pygtk_version
    if version >= (2,8,0):
        print_version = '.'.join([[str(i) for i in version]])
        out(print_version)
    else:
        out("NO")
except ImportError:
    out("NO")
except AttributeError:
    out("NO")
EOF
$PYTHON conftest.py
has_pygtk=`cat conftest.out`
rm -f conftest.out conftest.py
if test NO = "$has_pygtk"
then
   AC_MSG_ERROR([

**** The python bindings for gtk 2.8 (pygtk2>=2.8.0) could not be found.])
fi
AC_MSG_RESULT($has_pygtk)

AC_MSG_CHECKING(Python bindings for gnome)
cat > conftest.py <<EOF
$pygtk_require
try:
    import gnome
    # Do not import gnome.ui, this can kill python if the
    # display cannot be opened.  Just search it.
    import imp
    imp.find_module('gnome/ui')
    out("gnome.ui")
except ImportError:
    out("NO")
EOF
$PYTHON conftest.py
has_pygnome=`cat conftest.out`
rm -f conftest.out conftest.py
if test NO = "$has_pygnome"
then
   AC_MSG_ERROR([
**** The python bindings for gnome2 (gnome2-python) could not be found.])
fi
AC_MSG_RESULT($has_pygnome)

AC_MSG_CHECKING(Python bindings for gconf)
cat > conftest.py <<EOF
$pygtk_require
try:
    import gconf
    out("gconf")
except ImportError:
    import gnome.gconf
    out("gnome.gconf")
except ImportError:
    out("NO")
EOF
$PYTHON conftest.py
has_gconf=`cat conftest.out`
rm -f conftest.out conftest.py
if test NO = "$has_gconf"
then
   AC_MSG_ERROR([
**** The python bindings for gconf (gnome2-python-gconf) could not be found.])
fi
AC_MSG_RESULT($has_gconf)

AC_MSG_CHECKING(Python bindings for gnome vfs)
cat > conftest.py <<EOF
$pygtk_require
try:
    # Do not import gnome.vfs, this can raise a RuntimeError if the
    # display cannot be opened.  Just search it.
    import imp
    imp.find_module('gnome/vfs')
    out("gnome.vfs")
except ImportError:
    imp.find_module('gnomevfs')
    out("gnomevfs")
except:
    out("NO")
EOF
$PYTHON conftest.py
has_vfs=`cat conftest.out`
rm -f conftest.out conftest.py
if test NO = "$has_vfs"
then
   AC_MSG_ERROR([
**** The python bindings for GNOME VFS (gnome2-python-vfs) could not be found.])
fi
AC_MSG_RESULT($has_vfs)

AC_MSG_CHECKING(Python bindings for glade)
cat > conftest.py <<EOF
$pygtk_require
try:
    # Do not import gtk.glade, this can raise a RuntimeError if the
    # display cannot be opened.  Just search it.
    import imp
    imp.find_module('gtk/glade')
    out("gtk.glade")
except ImportError:
    out("NO")
EOF
$PYTHON conftest.py
has_pyglade=`cat conftest.out`
rm -f conftest.out conftest.py
if test NO = "$has_pyglade"
then
   AC_MSG_ERROR([

**** The python bindings for glade (pygtk2-libglade) could not be found.])
fi
AC_MSG_RESULT($has_pyglade)

AC_CONFIG_FILES([
po/Makefile.in
m4/Makefile
help/Makefile
help/output/Makefile
help/output/grampsfop.xconf
Makefile
gramps.spec
gramps.sh
src/const.py
src/Makefile
src/RelLib/Makefile
src/Config/Makefile
src/FilterEditor/Makefile
src/Mime/Makefile
src/DisplayTabs/Makefile
src/DisplayModels/Makefile
src/Selectors/Makefile
src/GrampsLogger/Makefile
src/GrampsDb/Makefile
src/Merge/Makefile
src/docgen/Makefile
src/Editors/Makefile
src/Filters/Makefile
src/Filters/Rules/Makefile
src/Filters/Rules/Person/Makefile
src/Filters/Rules/Family/Makefile
src/Filters/Rules/Event/Makefile
src/Filters/Rules/Source/Makefile
src/Filters/Rules/Place/Makefile
src/Filters/Rules/MediaObject/Makefile
src/Filters/Rules/Repository/Makefile
src/Filters/SideBar/Makefile
src/DataViews/Makefile
src/PluginUtils/Makefile
src/ReportBase/Makefile
src/plugins/Makefile
src/DateHandler/Makefile
src/data/Makefile
src/glade/Makefile
src/images/Makefile
data/Makefile
data/man/Makefile
data/man/fr/Makefile
example/Makefile
example/gramps/Makefile
])

AC_OUTPUT
